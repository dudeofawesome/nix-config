{
  pkgs,
  pkgs-stable,
  pkgs-unstable,
  lib,
  config,
  ...
}:

let
  inherit (lib) mkOption types;

  cfg = config.programs.vscode;
in
{
  options = {
    programs.vscode = {
      mutableUserSettings = mkOption {
        type = with types; nullOr path;
        default = null;
      };
    };
  };

  config =
    let
      user_dir =
        if pkgs.stdenv.targetPlatform.isDarwin then
          "$HOME/Library/Application Support/Code/User"
        else if pkgs.stdenv.targetPlatform.isLinux then
          "$HOME/.config/Code/User"
        else
          abort;
      config_file_path = "${user_dir}/settings.json";
      global_settings_db_path = "${user_dir}/globalStorage/state.vscdb";
    in

    lib.mkIf cfg.enable {
      home.activation = {
        createVSCodeMutableUserSettings = lib.mkIf (cfg.mutableUserSettings != null) (
          lib.hm.dag.entryAfter [ "writeBoundary" ] ''
            run mkdir -p "$(dirname ~/"${config_file_path}")"

            # Detect OS theme setting
            os_theme="$(/usr/bin/defaults read -g AppleInterfaceStyle 2> /dev/null || echo "Light")"
            # Read preferred color theme
            theme="$(grep -Po '(?<="workbench.preferred'"$os_theme"'ColorTheme": ").+(?=")' "${cfg.mutableUserSettings}")"

            # Escape slashes for later use in sed
            function escape_for_sed {
              echo "$1" | sed -Ee 's/\//\\\//gi'
            }

            prettier_path="$(escape_for_sed "${pkgs-unstable.nodePackages.prettier}/lib/node_modules/prettier/")"
            kubectl_path="$(escape_for_sed "${lib.getExe pkgs-stable.kubectl}")"
            helm_path="$(escape_for_sed "${lib.getExe pkgs-stable.kubernetes-helm}")"
            gitlab_ci_ls_path="$(escape_for_sed "${lib.getExe pkgs-unstable.gitlab-ci-ls}")"
            d2_path="$(escape_for_sed "${lib.getExe pkgs-unstable.d2}")"
            plantuml_path="$(escape_for_sed "${pkgs-unstable.plantuml}/lib/plantuml.jar")"
            # java_path="$(escape_for_sed "${pkgs-unstable.java}/lib/plantuml.jar")"

            run cat "${cfg.mutableUserSettings}" \
              `# Add header about nix` \
              | sed -e '1i \/\/ Generated by Home Manager' \
              \
              `# Modify the selected theme to prevent jerk` \
              | sed -Ee 's/("workbench.colorTheme"): "(.+)"/\1: "'"$theme"'"/i' \
              \
              `# Point to nix's Prettier` \
              | sed -Ee 's/("prettier.prettierPath"): "(.+)"/\1: "'"$prettier_path"'"/i' \
              `# Point to nix's kubectl` \
              | sed -Ee 's/("vscode-kubernetes.kubectl-path"): "(.+)"/\1: "'"$kubectl_path"'"/i' \
              | sed -Ee 's/("vs-kubernetes.kubectl-path"): "(.+)"/\1: "'"$kubectl_path"'"/i' \
              `# Point to nix's helm` \
              | sed -Ee 's/("vscode-kubernetes.helm-path"): "(.+)"/\1: "'"$helm_path"'"/i' \
              | sed -Ee 's/("vs-kubernetes.helm-path"): "(.+)"/\1: "'"$helm_path"'"/i' \
              `# Point to nix's gitlab-ci-ls` \
              | sed -Ee 's/("gitlabLs.executablePath"): "(.+)"/\1: "'"$gitlab_ci_ls_path"'"/i' \
              `# Point to nix's d2` \
              | sed -Ee 's/("D2.execPath"): "(.+)"/\1: "'"$d2_path"'"/i' \
              \
              `# Overwrite VS Code settings` \
              | run --quiet tee "${config_file_path}"
          ''
        );
      };
    };
}
